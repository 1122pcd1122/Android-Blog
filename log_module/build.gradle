if (Boolean.valueOf(rootProject.ext.isModule)){
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        if (Boolean.valueOf(rootProject.ext.isModule)){
            applicationId "activitytest.example.com.log_moudle"
        }
        minSdkVersion  rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]


        testInstrumentationRunner  rootProject.ext.dependencies["androidJUnitRunner"]


    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (Boolean.valueOf(rootProject.ext.isModule)) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures{
        dataBinding = true
    }
}

dependencies {

    implementation rootProject.ext.dependencies["appcompat"]
    implementation rootProject.ext.dependencies["material"]
    implementation rootProject.ext.dependencies["constraintlayout"]
    implementation "androidx.activity:activity-ktx:1.2.2"
    implementation rootProject.ext.dependencies["navigationFragmentKtx"]
    implementation rootProject.ext.dependencies["navigationUiKtx"]
    implementation rootProject.ext.dependencies["fragmentKtx"]
    implementation rootProject.ext.dependencies["paging"]
    implementation rootProject.ext.dependencies["coroutines"]


    implementation project(path: ':network_module')
    implementation project(path: ':base')

}